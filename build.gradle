plugins {
	id 'maven-publish'
	id 'fabric-loom' version '1.15-SNAPSHOT'
	id 'ploceus' version '1.15-SNAPSHOT'
}

base {
	archivesName = project.archives_base_name
}
version = "${project.version}+mc${project.minecraft_version}"
group = project.maven_group

ploceus {
	setIntermediaryGeneration(2)
}

repositories {
	maven { url = "https://matthewperiut.github.io/repository" }
	exclusiveContent {
		forRepository {
			maven {
				name = "Modrinth"
				url = "https://api.modrinth.com/maven"
			}
		}
		filter {
			includeGroup "maven.modrinth"
		}
	}
	maven {
		name = 'Jitpack'
		url = 'https://jitpack.io'
	}

	mavenCentral()
}

sourceSets {
	test {
		compileClasspath += main.compileClasspath + main.output
		runtimeClasspath += main.runtimeClasspath + main.output
	}
}

loom {
	mods {
		retroapi {
			sourceSet sourceSets.main
		}
		retroapi_test {
			sourceSet sourceSets.test
		}
	}
	runs {
		client {
			source sourceSets.test
		}
		server {
			source sourceSets.test
		}
	}
}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings ploceus.featherMappings(project.feather_build)

	// remove these lines if not using exceptions patches
	clientExceptions ploceus.raven(project.client_raven_build, 'client')
	serverExceptions ploceus.raven(project.server_raven_build, 'server')
	// remove these lines if not using generics patches
	clientSignatures ploceus.sparrow(project.client_sparrow_build, 'client')
	serverSignatures ploceus.sparrow(project.server_sparrow_build, 'server')
	// remove these lines if not using inner class patches
	clientNests ploceus.nests(project.client_nests_build, 'client')
	serverNests ploceus.nests(project.server_nests_build, 'server')

	ploceus.dependOsl(project.osl_version)

	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	modImplementation "com.periut:starac:${project.starac_version}"

	modCompileOnly("net.modificationstation:StationAPI:${project.stapi_version}")

	modLocalRuntime("com.periut:retrocommands:${project.retrocommands_version}") {
		transitive = false
	}

}
// Exclude Legacy Fabric LWJGL 2 wrapper (starac provides LWJGL 3)
configurations.configureEach {
	exclude group: 'org.lwjgl.lwjgl'
}

processResources {
	inputs.property 'version', version

	filesMatching('fabric.mod.json') {
		expand 'version': version
	}
}

processTestResources {
	inputs.property 'version', version

	filesMatching('fabric.mod.json') {
		expand 'version': version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.encoding = 'UTF-8'
	it.options.release = 21
}

java {
	withSourcesJar()
}

jar {
	from('LICENSE') {
		rename { "${it}_${base.archivesName.get()}" }
	}
}

test {
	// This is a mod testbed, not JUnit tests
	enabled = false
}

tasks.register('testJar', Jar) {
	from sourceSets.test.output
	archiveClassifier = 'testmod-dev'
}

tasks.register('remapTestJar', net.fabricmc.loom.task.RemapJarTask) {
	inputFile = tasks.named('testJar').flatMap { it.archiveFile }
	archiveClassifier = 'testmod'
	addNestedDependencies = false
}

build.dependsOn remapTestJar

publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}

	repositories {
		// Add repositories to publish to here.
	}
}
